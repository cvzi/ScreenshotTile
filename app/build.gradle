apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    signingConfigs {
        release {
            if (project.properties['keyStore.release.storeFile'] != null) {
                storeFile file(project.properties['keyStore.release.storeFile'])
                storePassword project.properties['keyStore.release.storePassword']
                keyAlias project.properties['keyStore.release.keyAlias']
                keyPassword project.properties['keyStore.release.keyPassword']
            } else {
                storeFile rootProject.file('mykey.jks')
                storePassword 'password'
                keyAlias 'key0'
                keyPassword 'password'
            }
        }
    }
    defaultConfig {
        applicationId 'com.github.cvzi.screenshottile'
        minSdk 24
        compileSdk 34
        targetSdk 34
        versionCode 113
        versionName '2.8.0'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField 'com.github.cvzi.screenshottile.MutableBoolean', 'TESTING_MODE', 'new com.github.cvzi.screenshottile.MutableBoolean(false)'
    }
    namespace 'com.github.cvzi.screenshottile'
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            debuggable false
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
            minifyEnabled false
            shrinkResources false
        }
    }
    productFlavors {
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = 17
    }
    testOptions {
        unitTests {
            includeAndroidResources true
        }
        unitTests.all {
            testLogging {
                showStandardStreams true
            }
        }
    }
    lint {
        disable 'MissingTranslation'
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation platform('org.jetbrains.kotlin:kotlin-bom:1.9.22')
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.viewpager:viewpager:1.0.0'
    implementation 'com.burhanrashid52:photoeditor:3.0.1'
    implementation 'com.google.android.material:material:1.12.0-alpha03'

    // Testing-only dependencies
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:core-ktx:1.5.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'androidx.test:core-ktx:1.5.0'
    testImplementation 'androidx.test.ext:junit:1.1.5'
    testImplementation 'androidx.test.ext:truth:1.5.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    testImplementation 'androidx.test.espresso:espresso-intents:3.5.1'
}
